apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'org.moallemi.advanced-build-version'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def apiKey = properties.getProperty('apiKey')

android {
  compileSdkVersion 24
  buildToolsVersion "24.0.3"

  defaultConfig {
    applicationId "com.pratamawijaya.mobileclimate"
    minSdkVersion 15
    targetSdkVersion 24
    versionCode 1
    versionName "1.0"
    buildConfigField 'String', 'APIKEY', "$apiKey"
  }
  splits {
    abi {
      enable true
      reset()
      include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a', 'mips' //select ABIs to build APKs for
      universalApk true //generate an additional APK that contains all the ABIs
    }
  }
  // map for the version code
  project.ext.versionCodes = ['armeabi'    : 1,
                              'armeabi-v7a': 2,
                              'arm64-v8a'  : 3,
                              'mips'       : 5,
                              'mips64'     : 6,
                              'x86'        : 8,
                              'x86_64'     : 9]

  android.applicationVariants.all { variant ->
    // assign different version code for each output
    variant.outputs.each { output ->
      output.versionCodeOverride =
          project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) *
              1000000 +
              android.defaultConfig.versionCode
    }
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

dependencies {
  final SUPPORT_VERSION = "24.2.1"
  testCompile 'junit:junit:4.12'
  compile "com.android.support:appcompat-v7:$SUPPORT_VERSION"
  compile "com.android.support:design:$SUPPORT_VERSION"
  compile "com.android.support:cardview-v7:$SUPPORT_VERSION"


  compile 'com.jakewharton:butterknife:7.0.1'

  compile 'com.google.code.gson:gson:2.6.2'
  compile 'com.squareup.retrofit2:retrofit:2.1.0'
  compile 'com.squareup.retrofit2:converter-gson:2.1.0'
  compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
  compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'

  compile 'io.reactivex:rxandroid:1.2.1'
  compile 'io.reactivex:rxjava:1.1.6'


  compile 'com.jakewharton.rxbinding:rxbinding:0.2.0'
  compile 'com.jakewharton.timber:timber:3.1.0'
}
